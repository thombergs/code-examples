buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springboot_version}"
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springcloudcontract_version}"
        classpath "org.springframework.cloud:spring-cloud-contract-spec-pact:${springcloudcontract_pact_version}"
        classpath 'au.com.dius:pact-jvm-model:2.4.18'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'

group = 'io.reflectoring'
version = '1.0.0'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springcloud_version}"
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile('com.h2database:h2:1.4.196')

    // add javassist >= 3.23.1-GA since earlier versions are broken in Java 11
    // see https://github.com/jboss-javassist/javassist/issues/194
    runtime('org.javassist:javassist:3.23.1-GA')

    // overriding bytebuddy to newer version that supports Java 11
    // see https://github.com/raphw/byte-buddy/issues/428
    testCompile('net.bytebuddy:byte-buddy:1.9.12')

    testCompile('org.codehaus.groovy:groovy-all:2.4.6')
    testCompile("org.springframework.cloud:spring-cloud-starter-contract-verifier")
    testCompile("org.springframework.cloud:spring-cloud-contract-spec:${springcloudcontract_pact_version}")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

contracts {
    baseClassMappings {
        baseClassMapping(".*userservice.*", "io.reflectoring.UserServiceBase")
    }
}

// a hack to locally publish the contract stubs when executing the build task
// so that it works in the CI build without a Nexus or Artifactory server
build.dependsOn('generatePomFileForStubsPublication')
build.dependsOn('publishStubsPublicationToMavenLocal')
